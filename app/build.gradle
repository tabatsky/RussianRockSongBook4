plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    signingConfigs {
        config {
            keyAlias 'test_key'
            keyPassword 'qwerty'
            storeFile file('..\\test.keystore')
            storePassword 'qwerty'
        }
    }

    compileSdk 34

    defaultConfig {
        applicationId "jatx.russianrocksongbook"
        minSdk 21
        targetSdk 34
        versionCode 105
        versionName "5.7.4"

        testInstrumentationRunner "jatx.russianrocksongbook.HiltTestRunner"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion versions.composeCompiler
    }
    packagingOptions {
        resources {
            excludes += [
                    'META-INF/AL2.0',
                    'META-INF/LGPL2.1',
                    'MANIFEST.MF',
                    'META-INF/LICENSE.md',
                    'META-INF/LICENSE-notice.md'
            ]
        }
    }
    namespace 'jatx.russianrocksongbook'
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

dependencies {
    implementation commonDeps.coroutines
    implementation commonDeps.coreKtx
    implementation commonDeps.material
    implementation commonDeps.activityCompose
    implementation commonDeps.composeUi
    implementation commonDeps.composeMaterial
    implementation commonDeps.composeUiTooling
    implementation commonDeps.navigationCompose
    implementation commonDeps.lifecycleRuntime
    implementation commonDeps.lifecycleViewmodel

    implementation diDeps.hiltAndroid
    kapt diDeps.hiltCompiler
    implementation diDeps.hiltExtensions
    kapt diDeps.hiltExtensionsProcessor

    api project(':presentation-layer:localSongs')
    api project(':presentation-layer:cloudSongs')
    api project(':presentation-layer:addArtist')
    api project(':presentation-layer:addSong')
    api project(':presentation-layer:donation')
    api project(':presentation-layer:settings')
    api project(':presentation-layer:start')

    api project(':presentation-layer:helpers:addSongsFromDirHelper')
    api project(':presentation-layer:helpers:donationHelper')
    api project(':presentation-layer:helpers:musicHelper')

    api project(':core:testing')

    // testing

    debugImplementation testDeps.mockkAndroid

    debugImplementation("androidx.test:core") {
        version {
            strictly versions.androidXTestCore
        }
    }

    testImplementation("androidx.test:core") {
        version {
            strictly versions.androidXTestCore
        }
    }
    testImplementation testDeps.androidXCoreKtx
    testImplementation testDeps.androidXRunner
    testImplementation testDeps.androidXTruth
    testImplementation testDeps.androidXRules

    androidTestImplementation("androidx.test:core") {
        version {
            strictly versions.androidXTestCore
        }
    }

    androidTestImplementation("androidx.test.ext:junit") {
        version {
            strictly versions.androidXJUnit
        }
    }

    androidTestImplementation testDeps.androidXCoreTesting

    debugImplementation testDeps.androidXMonitor

    androidTestImplementation testDeps.espressoCore
    androidTestImplementation(testDeps.espressoContrib) {
        exclude group: 'org.checkerframework', module: 'checker'
    }

    androidTestImplementation testDeps.hamcrest

    androidTestImplementation(testDeps.hiltAndroidTesting) {
        exclude group: 'androidx.test', module: 'core'
    }
    testImplementation(testDeps.hiltAndroidTesting) {
        exclude group: 'androidx.test', module: 'core'
    }
    kaptAndroidTest testDeps.hiltAndroidCompiler

    androidTestImplementation testDeps.composeUiTest
    androidTestImplementation(testDeps.composeUiTestJunit) {
        exclude group: 'androidx.text', module: 'core'
    }

    debugImplementation testDeps.composeUiTestManifest

    debugImplementation testDeps.kotlinReflect

    androidTestImplementation testDeps.leakCanary

    androidTestUtil testDeps.androidXOrchestrator
}