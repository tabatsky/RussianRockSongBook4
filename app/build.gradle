plugins {
    id 'com.android.application'
    alias libs.plugins.kotlin.android
    alias libs.plugins.kapt
    alias libs.plugins.hilt.android
    alias libs.plugins.compose
    alias libs.plugins.ksp
    alias libs.plugins.kotlin.serialization
    id 'com.google.gms.google-services'
}

android {
    signingConfigs {
        config {
            keyAlias 'test_key'
            keyPassword 'qwerty'
            storeFile file('..\\test.keystore')
            storePassword 'qwerty'
        }
    }

    compileSdk 34

    defaultConfig {
        applicationId "jatx.russianrocksongbook"
        minSdk 21
        targetSdk 34
        versionCode 111
        versionName "6.0.3"

        testInstrumentationRunner "jatx.russianrocksongbook.HiltTestRunner"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    packagingOptions {
        resources {
            excludes += [
                    'META-INF/AL2.0',
                    'META-INF/LGPL2.1',
                    'MANIFEST.MF',
                    'META-INF/LICENSE.md',
                    'META-INF/LICENSE-notice.md'
            ]
        }
    }
    namespace 'jatx.russianrocksongbook'
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

dependencies {
    implementation libs.coroutines
    implementation libs.coreKtx
    implementation libs.material
    implementation libs.activityCompose
    implementation libs.composeUi
    implementation libs.composeMaterial
    implementation libs.composeUiTooling
    implementation libs.navigationCompose
    implementation libs.lifecycleRuntime
    implementation libs.lifecycleViewmodel
    implementation libs.ktxSerializationCore

    implementation libs.hiltAndroid
    ksp libs.hiltCompiler
    implementation libs.hiltExtensions
    ksp libs.hiltExtensionsProcessor

    api project(':presentation:localSongs')
    api project(':presentation:cloudSongs')
    api project(':presentation:textSearch')
    api project(':presentation:addArtist')
    api project(':presentation:addSong')
    api project(':presentation:donation')
    api project(':presentation:settings')
    api project(':presentation:start')

    api project(':presentation:helpers:addSongsFromDirHelper')
    api project(':presentation:helpers:donationHelper')
    api project(':presentation:helpers:musicHelper')

    api project(':core:testing')

    implementation platform(libs.firebaseBom)
    implementation libs.firebaseAnalytics

    // testing

    debugImplementation libs.mockkAndroid

    debugImplementation("androidx.test:core") {
        version {
            strictly versions.androidXTestCore
        }
    }

    testImplementation("androidx.test:core") {
        version {
            strictly versions.androidXTestCore
        }
    }
    testImplementation libs.androidXCoreKtx
    testImplementation libs.androidXRunner
    testImplementation libs.androidXTruth
    testImplementation libs.androidXRules

    androidTestImplementation("androidx.test:core") {
        version {
            strictly versions.androidXTestCore
        }
    }

    androidTestImplementation("androidx.test.ext:junit") {
        version {
            strictly versions.androidXJUnit
        }
    }

    androidTestImplementation libs.androidXCoreTesting

    debugImplementation libs.androidXMonitor

    androidTestImplementation libs.espressoCore
    androidTestImplementation(libs.espressoContrib) {
        exclude group: 'org.checkerframework', module: 'checker'
    }

    androidTestImplementation libs.hamcrest

    androidTestImplementation(libs.hiltAndroidTesting) {
        exclude group: 'androidx.test', module: 'core'
    }
    testImplementation(libs.hiltAndroidTesting) {
        exclude group: 'androidx.test', module: 'core'
    }
    kspAndroidTest libs.hiltAndroidCompiler

    androidTestImplementation libs.composeUiTest
    androidTestImplementation(libs.composeUiTestJunit) {
        exclude group: 'androidx.text', module: 'core'
    }

    debugImplementation libs.composeUiTestManifest

    debugImplementation libs.kotlinReflect

    androidTestImplementation libs.leakCanary

    androidTestUtil libs.androidXOrchestrator
}